-******************************************************************************
-* Program:
-* Title:
-* Author:       Bill Schwartz
-* Date:
-* Description:
-******************************************************************************
-* Tables Accessed:
-* Tables Updated:
-******************************************************************************
-* Include/EX Files:
-******************************************************************************
-* Files Allocated:
-******************************************************************************
-* Change History:
-*     Programmer             Date              Description of Change
-*     ------------------  ----------           -------------------------------
-*
-******************************************************************************
-*-SET &ECHO = ALL;
-TYPE *************************************************************************
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL START &FOCFEXNAME.EVAL
-TYPE -------------------------------------------------------------------------
-IF  &DFLTCNTL.EXISTS EQ 1 THEN GOTO STEP001;
-******************************************************************************
-STEP000
-SET  &STEP       = '&FOCFEXNAME.EVAL'||'.'||'STEP000';
-SET  &NOWTIME    = HHMMSS(A8);
-TYPE &NOWTIME.EVAL &STEP.EVAL
-TYPE &NOWTIME.EVAL *** Establish APP PATH and defaults
-RUN
-*-----------------------------------------------------------------------------
-INCLUDE EPP2/EPP_DEFAULTS
-*-----------------------------------------------------------------------------
-SET  &STEP    = '&FOCFEXNAME.EVAL'||'.'||'STEP000';
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL &STEP.EVAL COMPLETE RETCODE&RETCODE.EVAL
-TYPE -------------------------------------------------------------------------
-IF   &FOCERRNUM.EVAL NE 0 THEN GOTO ERRORS;
-*-----------------------------------------------------------------------------
-STEP001
-SET  &STEP    = '&FOCFEXNAME.EVAL'||'.'||'STEP001';
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL &STEP.EVAL
-TYPE &NOWTIME.EVAL *** Write ISA 835 Transactions
-*-----------------------------------------------------------------------------
SQL
SELECT
      'ISA' "ISA"
     ,ISA_AUTH_INFO_QLFR
     ,ISA_AUTH_INFO
     ,ISA_SECUR_INFO_QLFR
     ,ISA_SECUR_INFO
     ,ISA_IC_SNDR_ID_QLFR
     ,ISA_IC_SNDR_ID
     ,ISA_IC_RCVR_ID_QLFR
     ,ISA_IC_RCVR_ID
     ,ISA_IC_DATETIME
     ,ISA_IC_CNTL_STD_ID
     ,ISA_IC_CNTL_VER_NO
     ,ISA_IC_CNTL_NO
     ,ISA_ACK_RQST
     ,ISA_TST_IND
     ,ISA_COMPO_SUPLMNT
FROM  &ISA.EVAL&ISA_TBL.EVAL
WHERE EXISTS
   ( SELECT 1
     FROM   &ST.EVAL&ST_TBL.EVAL
     WHERE  ST_EXCL_RSN_CD = '   ' )
ORDER BY
      ISA_IC_SNDR_ID;
TABLE FILE SQLOUT
PRINT
  COMPUTE C_REQ_TYPE/A4       = 'ISA*';
  COMPUTE C_GS_SEQ_NO/I5      = 0;
  COMPUTE C_ST_SEQ_NO/I7      = 0;
  COMPUTE C_REC_SEQ_IND/A2    = '00';
  COMPUTE C_LX_SEQ_NO/I4      = 0;
  COMPUTE C_CLP_SEQ_NO/I6     = 0;
  COMPUTE C_REC_SEQ_CLM/A2    = '00';
  COMPUTE C_SVC_SEQ_NO/I5     = 0;
  COMPUTE C_SERV_SEQ_IND/A2   = '00';
  COMPUTE C_SVC_CAS_SEQ_NO/I4 = 0;
  COMPUTE C_ALPH_DT/A20       =  HCNVRT(ISA_IC_DATETIME, '(HYYMDS)', 20, 'A20'); NOPRINT
  COMPUTE C_ISA_TIME/A4       = EDIT(C_ALPH_DT,'$$$$$$$$$$$99$99'); NOPRINT
  COMPUTE C_ISA_DT/A6         = EDIT(C_ALPH_DT,'$$99$99$99'); NOPRINT
  COMPUTE C_EDIVAL/A600V = C_REQ_TYPE || ISA_AUTH_INFO_QLFR | '*' |
                          ISA_AUTH_INFO | '*' | ISA_SECUR_INFO_QLFR | '*' |
                          ISA_SECUR_INFO | '*' | ISA_IC_SNDR_ID_QLFR | '*' |
                          ISA_IC_SNDR_ID | '*' | ISA_IC_RCVR_ID_QLFR | '*' |
                          ISA_IC_RCVR_ID | '*' | C_ISA_DT || '*' ||
                          C_ISA_TIME  || '*' ||
                          ISA_IC_CNTL_STD_ID || '*' || ISA_IC_CNTL_VER_NO || '*' ||
                          ISA_IC_CNTL_NO || '*' || ISA_ACK_RQST  || '*' ||
                          ISA_TST_IND || '*' || ISA_COMPO_SUPLMNT ; NOPRINT
  COMPUTE C_RMVSPC/A600V = TRIMV('T', C_EDIVAL, 600, ' ', 1, C_RMVSPC); NOPRINT
  COMPUTE C_EDIREC/A600V = TRIMV('T', C_RMVSPC, 600, '*', 1, C_EDIREC) | '~';
ON TABLE SET HOLDLIST PRINTONLY
ON TABLE HOLD AS EDITEMP FORMAT ALPHA
END
-RUN
-*-----------------------------------------------------------------------------
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL RECORDS &RECORDS.EVAL LINES &LINES.EVAL
-TYPE &NOWTIME.EVAL &STEP.EVAL COMPLETE RETCODE&RETCODE.EVAL
-TYPE -------------------------------------------------------------------------
-IF   &RETCODE.EVAL NE 0 THEN GOTO ERRORS;
-*-----------------------------------------------------------------------------
-GOTO THE_END;
-TYPE -------------------------------------------------------------------------
-ERRORS
-SET &ERRFEXNAME = '&FOCFEXNAME.EVAL';
-SET &ERRSTEP    = '&STEP.EVAL';
-SET &ERRRETCODE = &RETCODE.EVAL;
-SET &ERRLINES   = &LINES.EVAL;
-SET &ERRFOCERR  = &FOCERRNUM.EVAL;
-SET &ERROR_IND  = 'ERROR';
-RUN
-******************************************************************************
-THE_END
-******************************************************************************
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL FINISH &FOCFEXNAME.EVAL
-TYPE *************************************************************************
-RUN
