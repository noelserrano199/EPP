-******************************************************************************
-* Program:
-* Title:
-* Author:       Bill Schwartz
-* Date:
-* Description:
-******************************************************************************
-* Tables Accessed:
-* Tables Updated:
-******************************************************************************
-* Include/EX Files:
-******************************************************************************
-* Files Allocated:
-******************************************************************************
-* Change History:
-*     Programmer             Date              Description of Change
-*     ------------------  ----------           -------------------------------
-*
-******************************************************************************
-*-SET &ECHO = ALL;
-TYPE *************************************************************************
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL START &FOCFEXNAME.EVAL
-TYPE -------------------------------------------------------------------------
-IF  &DFLTCNTL.EXISTS EQ 1 THEN GOTO STEP001;
-******************************************************************************
-STEP000
-SET  &STEP       = '&FOCFEXNAME.EVAL'||'.'||'STEP000';
-SET  &NOWTIME    = HHMMSS(A8);
-TYPE &NOWTIME.EVAL &STEP.EVAL
-TYPE &NOWTIME.EVAL *** Establish APP PATH and defaults
-RUN
-*-----------------------------------------------------------------------------
-INCLUDE EPP2/EPP_DEFAULTS
-*-----------------------------------------------------------------------------
-SET  &STEP    = '&FOCFEXNAME.EVAL'||'.'||'STEP000';
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL &STEP.EVAL COMPLETE RETCODE&RETCODE.EVAL
-TYPE -------------------------------------------------------------------------
-IF   &FOCERRNUM.EVAL NE 0 THEN GOTO ERRORS;
-*-----------------------------------------------------------------------------
-STEP001
-SET  &STEP    = '&FOCFEXNAME.EVAL'||'.'||'STEP001';
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL &STEP.EVAL
-TYPE &NOWTIME.EVAL *** Write MOA 835 Transactions
-*-----------------------------------------------------------------------------
SQL
SELECT
      'MOA' "MOA"
     ,MOA_REIMBURES_RATE
     ,MOA_HCPCS_PYMT_AMT
     ,MOA_REMARK_CD1
     ,MOA_REMARK_CD2
     ,MOA_REMARK_CD3
     ,MOA_REMARK_CD4
     ,MOA_REMARK_CD5
     ,MOA_ESRD_PYMT_AMT
     ,MOA_NOPAY_PROF_AMT
     ,MOA_GS_GRP_SEQ_NO
     ,MOA_ST_SEQ_NO
     ,MOA_LX_SEQ_NO
     ,MOA_CLP_CLM_SEQ_NO
     ,MOA_SEQ_NO
FROM  &ST.EVAL&ST_TBL.EVAL
INNER JOIN
      &CLP.EVAL&CLP_TBL.EVAL
ON    CLP_ISA_FILE_ID    = ST_ISA_FILE_ID
  AND CLP_GS_GRP_SEQ_NO  = ST_GS_GRP_SEQ_NO
  AND CLP_ST_SEQ_NO      = ST_SEQ_NO
  AND CLP_EXCL_RSN_CD    = '   '
INNER JOIN
      &MOA.EVAL&MOA_TBL.EVAL
ON    MOA_ISA_FILE_ID    = CLP_ISA_FILE_ID
  AND MOA_GS_GRP_SEQ_NO  = CLP_GS_GRP_SEQ_NO
  AND MOA_ST_SEQ_NO      = CLP_ST_SEQ_NO
  AND MOA_LX_SEQ_NO      = CLP_LX_SEQ_NO
  AND MOA_CLP_CLM_SEQ_NO = CLP_CLM_SEQ_NO
  AND MOA_EXCL_RSN_CD    = '   '
WHERE ST_EXCL_RSN_CD     = '   '
ORDER BY
      MOA_GS_GRP_SEQ_NO
     ,MOA_ST_SEQ_NO
     ,MOA_LX_SEQ_NO
     ,MOA_CLP_CLM_SEQ_NO
     ,MOA_SEQ_NO
;
TABLE FILE SQLOUT
PRINT
  COMPUTE C_REQ_TYPE/A4       = 'MOA*';
  COMPUTE C_GS_SEQ_NO/I5      = MOA_GS_GRP_SEQ_NO;
  COMPUTE C_ST_CNTL_NO/I7     = MOA_ST_SEQ_NO;
  COMPUTE C_REC_SEQ_IND/A2    = '50';
  COMPUTE C_LX_SEQ_NO/I4      = MOA_LX_SEQ_NO;
  COMPUTE C_CLP_SEQ_NO/I6     = MOA_CLP_CLM_SEQ_NO;
  COMPUTE C_REC_SEQ_CLM/A2    = '22';
  COMPUTE C_SVC_SEQ_NO/I5     = MOA_SEQ_NO;
  COMPUTE C_SERV_SEQ_IND/A2   = '00';
  COMPUTE C_SVC_CAS_SEQ_NO/I4 = 0;
  COMPUTE C_EDIVAL/A600V = C_REQ_TYPE||MOA_REIMBURES_RATE|| '*' ||MOA_HCPCS_PYMT_AMT||'*'
    ||MOA_REMARK_CD1||'*'||MOA_REMARK_CD2||'*'||MOA_REMARK_CD3||'*'||MOA_REMARK_CD4|| '*'
    ||MOA_REMARK_CD5||'*'||MOA_ESRD_PYMT_AMT || '*' || MOA_NOPAY_PROF_AMT; NOPRINT
  COMPUTE C_RMVSPC/A600V = TRIMV('T', C_EDIVAL, 600, ' ', 1, C_RMVSPC); NOPRINT
  COMPUTE C_EDIREC/A600V = TRIMV('T', C_RMVSPC, 600, '*', 1, C_EDIREC) | '~';
ON TABLE SET HOLDLIST PRINTONLY
ON TABLE HOLD AS EDITEMP FORMAT ALPHA
END
-RUN
-*-----------------------------------------------------------------------------
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL RECORDS &RECORDS.EVAL LINES &LINES.EVAL
-TYPE &NOWTIME.EVAL &STEP.EVAL COMPLETE RETCODE&RETCODE.EVAL
-TYPE -------------------------------------------------------------------------
-IF   &RETCODE.EVAL NE 0 THEN GOTO ERRORS;
-*-----------------------------------------------------------------------------
-GOTO THE_END;
-TYPE -------------------------------------------------------------------------
-ERRORS
-SET &ERRFEXNAME = '&FOCFEXNAME.EVAL';
-SET &ERRSTEP    = '&STEP.EVAL';
-SET &ERRRETCODE = &RETCODE.EVAL;
-SET &ERRLINES   = &LINES.EVAL;
-SET &ERRFOCERR  = &FOCERRNUM.EVAL;
-SET &ERROR_IND  = 'ERROR';
-RUN
-******************************************************************************
-THE_END
-******************************************************************************
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL FINISH &FOCFEXNAME.EVAL
-TYPE *************************************************************************
-RUN
