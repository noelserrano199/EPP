-******************************************************************************-* Program:
-* Title:
-* Author:       Bill Schwartz
-* Date:
-* Description:
-******************************************************************************
-* Tables Accessed:
-* Tables Updated:
-******************************************************************************
-* Include/EX Files:
-******************************************************************************
-* Files Allocated:
-******************************************************************************
-* Change History:
-*  Programmer      Date      Description of Change
-* ------------  ----------  --------------------------------------------------
-* WLS           08/09/2013  Changed WHERE TOTAL in STEP002 to WHERE
-******************************************************************************
-*-SET &ECHO = ALL;
-TYPE *************************************************************************
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL START &FOCFEXNAME.EVAL
-TYPE -------------------------------------------------------------------------
-IF  &DFLTCNTL.EXISTS EQ 1 THEN GOTO STEP001;
-******************************************************************************
-STEP000
-SET  &STEP       = '&FOCFEXNAME.EVAL'||'.'||'STEP000';
-SET  &NOWTIME    = HHMMSS(A8);
-TYPE &NOWTIME.EVAL &STEP.EVAL
-TYPE &NOWTIME.EVAL *** Establish APP PATH and defaults
-RUN
-*-----------------------------------------------------------------------------
-INCLUDE EPP2/EPP_DEFAULTS
-*-----------------------------------------------------------------------------
-SET  &STEP    = '&FOCFEXNAME.EVAL'||'.'||'STEP000';
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL &STEP.EVAL COMPLETE RETCODE&RETCODE.EVAL
-TYPE -------------------------------------------------------------------------
-IF   &FOCERRNUM.EVAL NE 0 THEN GOTO ERRORS;
-*-----------------------------------------------------------------------------
-STEP001
-SET  &STEP    = '&FOCFEXNAME.EVAL'||'.'||'STEP001';
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL &STEP.EVAL
-TYPE &NOWTIME.EVAL *** Delete all rows from &NM1.EVAL&NM1_TBL.EVAL
-*-----------------------------------------------------------------------------
SQL
TRUNCATE TABLE &NM1.EVAL&NM1_TBL.EVAL;
COMMIT WORK;
END
-RUN
-*-----------------------------------------------------------------------------
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL &STEP.EVAL COMPLETE RETCODE&RETCODE.EVAL
-TYPE -------------------------------------------------------------------------
-IF   &RETCODE.EVAL NE 0 THEN GOTO ERRORS;
-*-----------------------------------------------------------------------------
-STEP002
-SET  &STEP    = '&FOCFEXNAME.EVAL'||'.'||'STEP002';
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL &STEP.EVAL
-TYPE &NOWTIME.EVAL *** Extract NM1 records from 835 EDI file
-*-----------------------------------------------------------------------------
DEFINE FILE EDI835_BY_RT_LX
FILE_ID/I10                              = &FILE_ID.EVAL;
GS_GRP_SEQ_NO/I5  WITH GS_FUNC_ID_CD     = GS_GRP_SEQ_NO + 1;
ST_SEQ_NO/I7  WITH ST_TXN_SET_CNTL_NO    = IF GS_GRP_SEQ_NO        EQ LAST GS_GRP_SEQ_NO
                                              THEN ST_SEQ_NO + 1
                                           ELSE 1;
LX_SEQ_NO/I4      WITH LX_ASGND_NO       = IF   FILE_ID            EQ LAST FILE_ID
                                            AND GS_GRP_SEQ_NO      EQ LAST GS_GRP_SEQ_NO
                                            AND ST_SEQ_NO          EQ LAST ST_SEQ_NO
                                                THEN LX_SEQ_NO + 1
                                           ELSE 1;
CLP_CLM_SEQ_NO/I6 WITH CLP_CLM_SUBMTR_ID = IF   FILE_ID            EQ LAST FILE_ID
                                            AND GS_GRP_SEQ_NO      EQ LAST GS_GRP_SEQ_NO
                                            AND ST_SEQ_NO          EQ LAST ST_SEQ_NO
                                            AND LX_SEQ_NO          EQ LAST LX_SEQ_NO
                                                THEN CLP_CLM_SEQ_NO + 1
                                           ELSE 1;
NM1_EPP_GEN_SEQ/I2                       = 0;
NM1_INCL_RSN_CD/A3                       = 'ORG';
NM1_EXCL_RSN_CD/A3                       = '   ';
END
-*
TABLE FILE EDI835_BY_RT_LX
PRINT NM1_ENTITY_TYP_QLFR  AS NM1_ENTITY_TYP_QLFR
      NM1_PT_LST_NM        AS NM1_PT_LST_NM
      NM1_PT_FST_NM        AS NM1_PT_FST_NM
      NM1_PT_MID_NM        AS NM1_PT_MID_NM
      NM1_PT_NM_PRFX       AS NM1_PT_NM_PRFX
      NM1_PT_NM_SFX        AS NM1_PT_NM_SFX
      NM1_INS_ID_CD_QLFR   AS NM1_INS_ID_CD_QLFR
      NM1_INS_ID_CD        AS NM1_INS_ID_CD
      NM1_ENTITY_REL_CD    AS NM1_ENTITY_REL_CD
      NM1_ENTITY_ID_CD2    AS NM1_ENTITY_ID_CD2
      NM1_ORG_NM           AS NM1_ORG_NM
      NM1_INCL_RSN_CD      AS NM1_INCL_RSN_CD
      NM1_EXCL_RSN_CD      AS NM1_EXCL_RSN_CD
      ISA_IC_DATE          NOPRINT
BY    FILE_ID              AS NM1_ISA_FILE_ID
BY    GS_GRP_SEQ_NO        AS NM1_GS_GRP_SEQ_NO
BY    ST_SEQ_NO            AS NM1_ST_SEQ_NO
BY    LX_SEQ_NO            AS NM1_LX_SEQ_NO
BY    CLP_CLM_SEQ_NO       AS NM1_CLP_CLM_SEQ_NO
BY    NM1_ENTITY_ID_CD     AS NM1_ENTITY_ID_CD
BY    NM1_EPP_GEN_SEQ      AS NM1_EPP_GEN_SEQ
WHERE NM1_ENTITY_ID_CD IS NOT MISSING;
ON TABLE SET ALL ON
ON TABLE SET ASNAMES ON
ON TABLE SET HOLDLIST PRINTONLY
ON TABLE HOLD AS 835RECS
END
-RUN
-*-----------------------------------------------------------------------------
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL RECORDS &RECORDS.EVAL LINES &LINES.EVAL
-TYPE &NOWTIME.EVAL &STEP.EVAL COMPLETE RETCODE&RETCODE.EVAL
-TYPE -------------------------------------------------------------------------
-IF   &RETCODE.EVAL NE 0 THEN GOTO ERRORS;
-IF   &RECORDS.EVAL EQ 0 THEN GOTO ERRORS;
-*-----------------------------------------------------------------------------
-STEP003
-SET  &STEP    = '&FOCFEXNAME.EVAL'||'.'||'STEP003';
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL &STEP.EVAL
-TYPE &NOWTIME.EVAL *** Insert NM1 rows to &NM1.EVAL&NM1_TBL.EVAL
-*-----------------------------------------------------------------------------
MODIFY FILE &NM1_TBL.EVAL
SQL SET LOADONLY
MATCH NM1_ISA_FILE_ID NM1_GS_GRP_SEQ_NO NM1_ST_SEQ_NO NM1_LX_SEQ_NO NM1_CLP_CLM_SEQ_NO NM1_EPP_GEN_SEQ NM1_ENTITY_ID_CD
ON MATCH   REJECT
ON NOMATCH INCLUDE
FIXFORM FROM 835RECS
DATA ON 835RECS
LOG FORMAT ON LOGFMT MSG OFF
END
-RUN
-*-----------------------------------------------------------------------------
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL TRANS &TRANS.EVAL INPUT &INPUT.EVAL
-TYPE &NOWTIME.EVAL &STEP.EVAL COMPLETE RETCODE&RETCODE.EVAL
-TYPE -------------------------------------------------------------------------
-IF   &RETCODE.EVAL NE 0           THEN GOTO ERRORS;
-IF   &INPUT.EVAL   NE &TRANS.EVAL THEN GOTO ERRORS;
-*-----------------------------------------------------------------------------
-GOTO THE_END;
-TYPE -------------------------------------------------------------------------
-ERRORS
-SET &ERRFEXNAME = '&FOCFEXNAME.EVAL';
-SET &ERRSTEP    = '&STEP.EVAL';
-SET &ERRRETCODE = &RETCODE.EVAL;
-SET &ERRLINES   = &LINES.EVAL;
-SET &ERRFOCERR  = &FOCERRNUM.EVAL;
-SET &ERROR_IND  = 'ERROR';
-RUN
-******************************************************************************
-THE_END
-******************************************************************************
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL FINISH &FOCFEXNAME.EVAL
-TYPE *************************************************************************
-RUN
