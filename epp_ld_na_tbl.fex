-******************************************************************************-* Program:
-* Title:
-* Author:       Bill Schwartz
-* Date:
-* Description:
-******************************************************************************
-* Tables Accessed:
-* Tables Updated:
-******************************************************************************
-* Include/EX Files:
-******************************************************************************
-* Files Allocated:
-******************************************************************************
-* Change History:
-*  Programmer      Date      Description of Change
-* ------------  ----------  --------------------------------------------------
-* WLS           08/09/2013  Changed WHERE TOTAL in STEP002 to WHERE
-******************************************************************************
-*-SET &ECHO = ALL;
-TYPE *************************************************************************
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL START &FOCFEXNAME.EVAL
-TYPE -------------------------------------------------------------------------
-IF  &DFLTCNTL.EXISTS EQ 1 THEN GOTO STEP001;
-******************************************************************************
-STEP000
-SET  &STEP       = '&FOCFEXNAME.EVAL'||'.'||'STEP000';
-SET  &NOWTIME    = HHMMSS(A8);
-TYPE &NOWTIME.EVAL &STEP.EVAL
-TYPE &NOWTIME.EVAL *** Establish APP PATH and defaults
-RUN
-*-----------------------------------------------------------------------------
-INCLUDE EPP2/EPP_DEFAULTS
-*-----------------------------------------------------------------------------
-SET  &STEP    = '&FOCFEXNAME.EVAL'||'.'||'STEP000';
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL &STEP.EVAL COMPLETE RETCODE&RETCODE.EVAL
-TYPE -------------------------------------------------------------------------
-IF   &FOCERRNUM.EVAL NE 0 THEN GOTO ERRORS;
-*-----------------------------------------------------------------------------
-STEP001
-SET  &STEP    = '&FOCFEXNAME.EVAL'||'.'||'STEP001';
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL &STEP.EVAL
-TYPE &NOWTIME.EVAL *** Delete all rows from &NA.EVAL&NA_TBL.EVAL
-*-----------------------------------------------------------------------------
SQL
DELETE FROM &NA.EVAL&NA_TBL.EVAL;
COMMIT WORK;
END
-RUN
-*-----------------------------------------------------------------------------
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL &STEP.EVAL COMPLETE RETCODE&RETCODE.EVAL
-TYPE -------------------------------------------------------------------------
-IF   &RETCODE.EVAL NE 0 THEN GOTO ERRORS;
-*-----------------------------------------------------------------------------
-STEP002
-SET  &STEP    = '&FOCFEXNAME.EVAL'||'.'||'STEP002';
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL &STEP.EVAL
-TYPE &NOWTIME.EVAL *** Extract NA records from 835 EDI file
-*-----------------------------------------------------------------------------
DEFINE FILE EDI835_BY_RT
FILE_ID/I10                           = &FILE_ID.EVAL;
GS_GRP_SEQ_NO/I5 WITH GS_FUNC_ID_CD   = GS_GRP_SEQ_NO + 1;
ST_SEQ_NO/I7  WITH ST_TXN_SET_CNTL_NO = IF GS_GRP_SEQ_NO        EQ LAST GS_GRP_SEQ_NO
                                           THEN ST_SEQ_NO + 1
                                        ELSE 1;
N1_SEQ_NO/I4     WITH N1_ENTITY_ID_CD = IF   FILE_ID            EQ LAST FILE_ID
                                         AND GS_GRP_SEQ_NO      EQ LAST GS_GRP_SEQ_NO
                                         AND ST_SEQ_NO          EQ LAST ST_SEQ_NO
                                             THEN N1_SEQ_NO + 1
                                        ELSE 1;
NA_N3_RT/A4                           = N3_RT;
NA_N4_RT/A4                           = N4_RT;
END
-*
TABLE FILE EDI835_BY_RT
PRINT N1_ENTITY_NAME       AS NA_N1_ENTITY_NAME
      N1_ID_CD_QLFR        AS NA_N1_ID_CD_QLFR
      N1_ID_CD             AS NA_N1_ID_CD
      N1_ENTITY_REL_CD     AS NA_N1_ENTITY_REL_CD
      N1_ENTITY_REL_ID_CD  AS NA_N1_ENTITY_REL_ID_CD
      NA_N3_RT             AS NA_N3_RT
      N3_ADDR_LINE1        AS NA_N3_ADDR_LINE1
      N3_ADDR_LINE2        AS NA_N3_ADDR_LINE2
      NA_N4_RT             AS NA_N4_RT
      N4_CITY_NAME         AS NA_N4_CITY_NAME
      N4_STATE             AS NA_N4_STATE
      N4_POSTAL_CD         AS NA_N4_POSTAL_CD
      N4_COUNTRY_CD        AS NA_N4_COUNTRY_CD
      N4_LOC_QLFR          AS NA_N4_LOC_QLFR
      N4_LOC_ID            AS NA_N4_LOC_ID
      N4_COUNTRY_SUBDIV_CD AS NA_N4_COUNTRY_SUBDIV_CD
      ISA_IC_DATE          NOPRINT
BY    FILE_ID              AS NA_ISA_FILE_ID
BY    GS_GRP_SEQ_NO        AS NA_GS_GRP_SEQ_NO
BY    ST_SEQ_NO            AS NA_ST_SEQ_NO
BY    N1_ENTITY_ID_CD      AS NA_N1_ENTITY_ID_CD
BY    N1_SEQ_NO            AS NA_N1_SEQ_NO
WHERE TOTAL N1_ENTITY_ID_CD IS NOT MISSING;
ON TABLE SET ALL ON
ON TABLE SET ASNAMES ON
ON TABLE SET HOLDLIST PRINTONLY
ON TABLE HOLD AS 835RECS
END
-RUN
-*-----------------------------------------------------------------------------
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL RECORDS &RECORDS.EVAL LINES &LINES.EVAL
-TYPE &NOWTIME.EVAL &STEP.EVAL COMPLETE RETCODE&RETCODE.EVAL
-TYPE -------------------------------------------------------------------------
-IF   &RETCODE.EVAL NE 0 THEN GOTO ERRORS;
-IF   &LINES.EVAL   EQ 0 THEN GOTO THE_END;
-*-----------------------------------------------------------------------------
-STEP003
-SET  &STEP    = '&FOCFEXNAME.EVAL'||'.'||'STEP003';
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL &STEP.EVAL
-TYPE &NOWTIME.EVAL *** Insert NA rows to &NA.EVAL&NA_TBL.EVAL
-*-----------------------------------------------------------------------------
MODIFY FILE &NA_TBL.EVAL
SQL SET LOADONLY
MATCH NA_ISA_FILE_ID NA_GS_GRP_SEQ_NO NA_ST_SEQ_NO NA_N1_ENTITY_ID_CD NA_N1_SEQ_NO
ON MATCH   REJECT
ON NOMATCH INCLUDE
FIXFORM FROM 835RECS
DATA ON 835RECS
LOG FORMAT ON LOGFMT MSG OFF
END
-RUN
-*-----------------------------------------------------------------------------
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL TRANS &TRANS.EVAL INPUT &INPUT.EVAL
-TYPE &NOWTIME.EVAL &STEP.EVAL COMPLETE RETCODE&RETCODE.EVAL
-TYPE -------------------------------------------------------------------------
-IF   &RETCODE.EVAL NE 0           THEN GOTO ERRORS;
-IF   &INPUT.EVAL   NE &TRANS.EVAL THEN GOTO ERRORS;
-*-----------------------------------------------------------------------------
-GOTO THE_END;
-TYPE -------------------------------------------------------------------------
-ERRORS
-SET &ERRFEXNAME = '&FOCFEXNAME.EVAL';
-SET &ERRSTEP    = '&STEP.EVAL';
-SET &ERRRETCODE = &RETCODE.EVAL;
-SET &ERRLINES   = &LINES.EVAL;
-SET &ERRFOCERR  = &FOCERRNUM.EVAL;
-SET &ERROR_IND  = 'ERROR';
-RUN
-INCLUDE GBL_ERRORS2
-******************************************************************************
-THE_END
-******************************************************************************
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL FINISH &FOCFEXNAME.EVAL
-TYPE *************************************************************************
-RUN
