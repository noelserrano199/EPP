-******************************************************************************-* Program:
-* Title:
-* Author:       Bill Schwartz
-* Date:
-* Description:
-******************************************************************************
-* Tables Accessed:
-* Tables Updated:
-******************************************************************************
-* Include/EX Files:
-******************************************************************************
-* Files Allocated:
-******************************************************************************
-* Change History:
-*  Programmer      Date      Description of Change
-* ------------  ----------  --------------------------------------------------
-* WLS           08/09/2013  Changed WHERE TOTAL in STEP002 to WHERE
-******************************************************************************
-*SET &ECHO = ALL;
-TYPE *************************************************************************
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL START &FOCFEXNAME.EVAL
-TYPE -------------------------------------------------------------------------
-IF  &DFLTCNTL.EXISTS EQ 1 THEN GOTO STEP001;
-******************************************************************************
-STEP000
-SET  &STEP       = '&FOCFEXNAME.EVAL'||'.'||'STEP000';
-SET  &NOWTIME    = HHMMSS(A8);
-TYPE &NOWTIME.EVAL &STEP.EVAL
-TYPE &NOWTIME.EVAL *** Establish APP PATH and defaults
-RUN
-*-----------------------------------------------------------------------------
-INCLUDE EPP2/EPP_DEFAULTS
-*-----------------------------------------------------------------------------
-SET  &STEP    = '&FOCFEXNAME.EVAL'||'.'||'STEP000';
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL &STEP.EVAL COMPLETE RETCODE &RETCODE.EVAL
-TYPE -------------------------------------------------------------------------
-IF   &FOCERRNUM.EVAL NE 0 THEN GOTO ERRORS;
-*-----------------------------------------------------------------------------
-STEP001
-SET  &STEP    = '&FOCFEXNAME.EVAL'||'.'||'STEP001';
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL &STEP.EVAL
-TYPE &NOWTIME.EVAL *** Delete all rows from &TS3.EVAL&TS3_TBL.EVAL
-*-----------------------------------------------------------------------------
SQL
TRUNCATE TABLE &TS3.EVAL&TS3_TBL.EVAL;
COMMIT WORK;
END
-RUN
-*-----------------------------------------------------------------------------
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL &STEP.EVAL COMPLETE RETCODE &RETCODE.EVAL
-TYPE -------------------------------------------------------------------------
-IF   &RETCODE.EVAL NE 0 THEN GOTO ERRORS;
-*-----------------------------------------------------------------------------
-STEP002
-SET  &STEP    = '&FOCFEXNAME.EVAL'||'.'||'STEP002';
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL &STEP.EVAL
-*-----------------------------------------------------------------------------
DEFINE FILE EDI835_BY_RT_LX
FILE_ID/I10                              = &FILE_ID.EVAL;
GS_GRP_SEQ_NO/I5 WITH GS_FUNC_ID_CD      = GS_GRP_SEQ_NO + 1;
ST_SEQ_NO/I7  WITH ST_TXN_SET_CNTL_NO    = IF GS_GRP_SEQ_NO        EQ LAST GS_GRP_SEQ_NO
                                              THEN ST_SEQ_NO + 1
                                           ELSE 1;
LX_SEQ_NO/I4     WITH LX_ASGND_NO        = IF   FILE_ID            EQ LAST FILE_ID
                                            AND GS_GRP_SEQ_NO      EQ LAST GS_GRP_SEQ_NO
                                            AND ST_SEQ_NO          EQ LAST ST_SEQ_NO
                                                THEN LX_SEQ_NO + 1
                                           ELSE 1;
TS3_SEQ_NO/I5     WITH TS3_REF_ID        = IF   FILE_ID            EQ LAST FILE_ID
                                            AND GS_GRP_SEQ_NO      EQ LAST GS_GRP_SEQ_NO
                                            AND ST_SEQ_NO          EQ LAST ST_SEQ_NO
                                            AND LX_SEQ_NO          EQ LAST LX_SEQ_NO
                                                THEN TS3_SEQ_NO + 1
                                           ELSE 1;
END
-*
TABLE FILE EDI835_BY_RT_LX
PRINT TS3_REF_ID           AS TS3_REF_ID
      TS3_FACILITY_CD      AS TS3_FACILITY_CD
      TS3_DATE             AS TS3_DATE
      TS3_QTY01            AS TS3_QTY01
      TS3_AMT01            AS TS3_AMT01
      TS3_AMT02            AS TS3_AMT02
      TS3_AMT03            AS TS3_AMT03
      TS3_AMT04            AS TS3_AMT04
      TS3_AMT05            AS TS3_AMT05
      TS3_AMT06            AS TS3_AMT06
      TS3_AMT07            AS TS3_AMT07
      TS3_AMT08            AS TS3_AMT08
      TS3_AMT09            AS TS3_AMT09
      TS3_AMT10            AS TS3_AMT10
      TS3_AMT11            AS TS3_AMT11
      TS3_AMT12            AS TS3_AMT12
      TS3_AMT13            AS TS3_AMT13
      TS3_AMT14            AS TS3_AMT14
      TS3_AMT15            AS TS3_AMT15
      TS3_AMT16            AS TS3_AMT16
      TS3_AMT17            AS TS3_AMT17
      TS3_AMT18            AS TS3_AMT18
      TS3_QTY02            AS TS3_QTY02
      TS3_AMT19            AS TS3_AMT19
      ISA_IC_DATE          NOPRINT
BY    FILE_ID              AS TS3_ISA_FILE_ID
BY    GS_GRP_SEQ_NO        AS TS3_GS_GRP_SEQ_NO
BY    ST_SEQ_NO            AS TS3_ST_SEQ_NO
BY    LX_SEQ_NO            AS TS3_LX_SEQ_NO
BY    TS3_SEQ_NO           AS TS3_SEQ_NO
WHERE TS3_REF_ID IS NOT MISSING;
ON TABLE SET ALL ON
ON TABLE SET ASNAMES ON
ON TABLE SET HOLDLIST PRINTONLY
ON TABLE HOLD AS 835RECS
END
-RUN
-*-----------------------------------------------------------------------------
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL RECORDS &RECORDS.EVAL LINES &LINES.EVAL
-TYPE &NOWTIME.EVAL &STEP.EVAL COMPLETE RETCODE &RETCODE.EVAL
-TYPE -------------------------------------------------------------------------
-IF   &RETCODE.EVAL NE 0 THEN GOTO ERRORS;
-IF   &LINES.EVAL   EQ 0 THEN GOTO THE_END;
-*-----------------------------------------------------------------------------
-STEP003
-SET  &STEP    = '&FOCFEXNAME.EVAL'||'.'||'STEP003';
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL &STEP.EVAL
-TYPE &NOWTIME.EVAL *** Insert TS3 rows to &TS3.EVAL&TS3_TBL.EVAL
-*-----------------------------------------------------------------------------
MODIFY FILE &TS3_TBL.EVAL
SQL SET LOADONLY
MATCH TS3_ISA_FILE_ID TS3_GS_GRP_SEQ_NO TS3_ST_SEQ_NO TS3_LX_SEQ_NO TS3_SEQ_NO
ON MATCH   REJECT
ON NOMATCH INCLUDE
FIXFORM FROM 835RECS
DATA ON 835RECS
LOG FORMAT ON LOGFMT MSG OFF
END
-RUN
-*-----------------------------------------------------------------------------
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL TRANS &TRANS.EVAL INPUT &INPUT.EVAL
-TYPE &NOWTIME.EVAL &STEP.EVAL COMPLETE RETCODE&RETCODE.EVAL
-TYPE -------------------------------------------------------------------------
-IF   &RETCODE.EVAL NE 0           THEN GOTO ERRORS;
-IF   &INPUT.EVAL   NE &TRANS.EVAL THEN GOTO ERRORS;
-*-----------------------------------------------------------------------------
-GOTO THE_END;
-TYPE -------------------------------------------------------------------------
-ERRORS
-SET &ERRFEXNAME = '&FOCFEXNAME.EVAL';
-SET &ERRSTEP    = '&STEP.EVAL';
-SET &ERRRETCODE = &RETCODE.EVAL;
-SET &ERRLINES   = &LINES.EVAL;
-SET &ERRFOCERR  = &FOCERRNUM.EVAL;
-SET &ERROR_IND  = 'ERROR';
-RUN
-******************************************************************************
-THE_END
-******************************************************************************
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL FINISH &FOCFEXNAME.EVAL
-TYPE *************************************************************************
-RUN
