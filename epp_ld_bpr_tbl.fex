-******************************************************************************-* Program:
-* Title:
-* Author:       Bill Schwartz
-* Date:
-* Description:
-******************************************************************************
-* Tables Accessed:
-* Tables Updated:
-******************************************************************************
-* Include/EX Files:
-******************************************************************************
-* Files Allocated:
-******************************************************************************
-* Change History:
-*  Programmer      Date      Description of Change
-* ------------  ----------  --------------------------------------------------
-* WLS           08/09/2013  Changed WHERE TOTAL in STEP002 to WHERE
-******************************************************************************
-*-SET &ECHO = ALL;
-TYPE *************************************************************************
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL START &FOCFEXNAME.EVAL
-TYPE -------------------------------------------------------------------------
-IF  &DFLTCNTL.EXISTS EQ 1 THEN GOTO STEP001;
-******************************************************************************
-STEP000
-SET  &STEP       = '&FOCFEXNAME.EVAL'||'.'||'STEP000';
-SET  &NOWTIME    = HHMMSS(A8);
-TYPE &NOWTIME.EVAL &STEP.EVAL
-TYPE &NOWTIME.EVAL *** Establish APP PATH and defaults
-RUN
-*-----------------------------------------------------------------------------
-INCLUDE EPP2/EPP_DEFAULTS
-*-----------------------------------------------------------------------------
-SET  &STEP    = '&FOCFEXNAME.EVAL'||'.'||'STEP000';
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL &STEP.EVAL COMPLETE RETCODE&RETCODE.EVAL
-TYPE -------------------------------------------------------------------------
-IF   &FOCERRNUM.EVAL NE 0 THEN GOTO ERRORS;
-*-----------------------------------------------------------------------------
-STEP001
-SET  &STEP    = '&FOCFEXNAME.EVAL'||'.'||'STEP001';
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL &STEP.EVAL
-TYPE &NOWTIME.EVAL *** Delete all rows from &BPR.EVAL&BPR_TBL.EVAL
-*-----------------------------------------------------------------------------
SQL
TRUNCATE TABLE &BPR.EVAL&BPR_TBL.EVAL;
COMMIT WORK;
END
-RUN
-*-----------------------------------------------------------------------------
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL &STEP.EVAL COMPLETE RETCODE&RETCODE.EVAL
-TYPE -------------------------------------------------------------------------
-IF   &RETCODE.EVAL NE 0 THEN GOTO ERRORS;
-*-----------------------------------------------------------------------------
-STEP002
-SET  &STEP    = '&FOCFEXNAME.EVAL'||'.'||'STEP002';
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL &STEP.EVAL
-TYPE &NOWTIME.EVAL *** Extract BPR records from 835 EDI file
-*-----------------------------------------------------------------------------
DEFINE FILE EDI835_BY_RT
FILE_ID/I8            = &FILE_ID.EVAL;
GS_GRP_SEQ_NO/I5 WITH GS_FUNC_ID_CD   = GS_GRP_SEQ_NO + 1;
ST_SEQ_NO/I09 WITH ST_TXN_SET_CNTL_NO = IF GS_GRP_SEQ_NO EQ LAST GS_GRP_SEQ_NO
                                             THEN ST_SEQ_NO + 1
                                        ELSE 1;
BPR_EPP_GEN_SEQ/I2     = 0;
BPR_INCL_RSN_CD/A3     = 'ORG';
BPR_EXCL_RSN_CD/A3     = '   ';
END
-*
TABLE FILE EDI835_BY_RT
PRINT BPR_TXN_HANDL_CD     AS BPR_TXN_HANDL_CD
      BPR_CHK_AMT          AS BPR_CHK_AMT
      BPR_CRDT_DBT_FLG_CD  AS BPR_CRDT_DBT_FLG_CD
      BPR_PMT_METHOD_CD    AS BPR_PMT_METHOD_CD
      BPR_PMT_FORMAT_CD    AS BPR_PMT_FORMAT_CD
      BPR_DFI_ID_NO_QLFR1  AS BPR_DFI_ID_NO_QLFR1
      BPR_DFI_ID_NO1       AS BPR_DFI_ID_NO1
      BPR_ACCT_NO_QLFR1    AS BPR_ACCT_NO_QLFR1
      BPR_ACCT_NO1         AS BPR_ACCT_NO1
      BPR_ORIG_CO_ID       AS BPR_ORIG_CO_ID
      BPR_ORIG_CO_SUP_CD   AS BPR_ORIG_CO_SUP_CD
      BPR_DFI_ID_NO_QLFR2  AS BPR_DFI_ID_NO_QLFR2
      BPR_DFI_ID_NO2       AS BPR_DFI_ID_NO2
      BPR_ACCT_NO_QLFR2    AS BPR_ACCT_NO_QLFR2
      BPR_ACCOUNT_NO2      AS BPR_ACCOUNT_NO2
      BPR_CHK_DT           AS BPR_CHK_DT
      BPR_BUS_FUNC_CD      AS BPR_BUS_FUNC_CD
      BPR_DFI_ID_NO_QLFR3  AS BPR_DFI_ID_NO_QLFR3
      BPR_DFI_ID_NO3       AS BPR_DFI_ID_NO3
      BPR_ACCOT_NO_QLFR3   AS BPR_ACCOT_NO_QLFR3
      BPR_ACCT_NO3         AS BPR_ACCT_NO3
      BPR_INCL_RSN_CD      AS BPR_INCL_RSN_CD
      BPR_EXCL_RSN_CD      AS BPR_EXCL_RSN_CD
      ISA_IC_DATE          NOPRINT
BY    FILE_ID              AS BPR_ISA_FILE_ID
BY    GS_GRP_SEQ_NO        AS BPR_GS_GRP_SEQ_NO
BY    ST_SEQ_NO            AS BPR_ST_SEQ_NO
BY    BPR_EPP_GEN_SEQ      AS BPR_EPP_GEN_SEQ
WHERE BPR_TXN_HANDL_CD IS NOT MISSING;
ON TABLE SET ALL ON
ON TABLE SET ASNAMES ON
ON TABLE SET HOLDLIST PRINTONLY
ON TABLE HOLD AS 835RECS
END
-RUN
-*-----------------------------------------------------------------------------
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL RECORDS &RECORDS.EVAL LINES &LINES.EVAL
-TYPE &NOWTIME.EVAL &STEP.EVAL COMPLETE RETCODE&RETCODE.EVAL
-TYPE -------------------------------------------------------------------------
-IF   &RETCODE.EVAL NE 0 THEN GOTO ERRORS;
-IF   &LINES.EVAL   EQ 0 THEN GOTO THE_END;
-*-----------------------------------------------------------------------------
-STEP003
-SET  &STEP    = '&FOCFEXNAME.EVAL'||'.'||'STEP003';
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL &STEP.EVAL
-TYPE &NOWTIME.EVAL *** Insert BPR rows to &BPR.EVAL&BPR_TBL.EVAL
-*-----------------------------------------------------------------------------
MODIFY FILE &BPR_TBL.EVAL
SQL SET LOADONLY
MATCH BPR_ISA_FILE_ID BPR_GS_GRP_SEQ_NO BPR_ST_SEQ_NO BPR_EPP_GEN_SEQ
ON MATCH   REJECT
ON NOMATCH INCLUDE
FIXFORM FROM 835RECS
DATA ON 835RECS
LOG FORMAT ON LOGFMT  MSG OFF
END
-RUN
-*-----------------------------------------------------------------------------
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL TRANS &TRANS.EVAL INPUT &INPUT.EVAL
-TYPE &NOWTIME.EVAL &STEP.EVAL COMPLETE RETCODE&RETCODE.EVAL
-TYPE -------------------------------------------------------------------------
-IF   &RETCODE.EVAL NE 0           THEN GOTO ERRORS;
-IF   &INPUT.EVAL   NE &TRANS.EVAL THEN GOTO ERRORS;
-*-----------------------------------------------------------------------------
-GOTO THE_END;
-TYPE -------------------------------------------------------------------------
-ERRORS
-SET &ERRFEXNAME = '&FOCFEXNAME.EVAL';
-SET &ERRSTEP    = '&STEP.EVAL';
-SET &ERRRETCODE = &RETCODE.EVAL;
-SET &ERRLINES   = &LINES.EVAL;
-SET &ERRFOCERR  = &FOCERRNUM.EVAL;
-SET &ERROR_IND  = 'ERROR';
-RUN
-******************************************************************************
-THE_END
-******************************************************************************
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL FINISH &FOCFEXNAME.EVAL
-TYPE *************************************************************************
-RUN
