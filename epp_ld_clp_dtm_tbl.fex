-******************************************************************************-* Program:
-* Title:
-* Author:       Bill Schwartz
-* Date:
-* Description:
-******************************************************************************
-* Tables Accessed:
-* Tables Updated:
-******************************************************************************
-* Include/EX Files:
-******************************************************************************
-* Files Allocated:
-******************************************************************************
-* Change History:
-* Programmer    Date       Description of Change
-* ---------- ----------  --------------------------------------------------
-* WLS        08/09/2013  Changed WHERE TOTAL in STEP002 to WHERE
-* WLS        10/19/2013  Added EPP_GEN_SEQ/include/exclude rsn_cd to CLP_DTM
-******************************************************************************
-*-SET &ECHO = ALL;
-TYPE *************************************************************************
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL START &FOCFEXNAME.EVAL
-TYPE -------------------------------------------------------------------------
-IF  &DFLTCNTL.EXISTS EQ 1 THEN GOTO STEP001;
-******************************************************************************
-STEP000
-SET  &STEP       = '&FOCFEXNAME.EVAL'||'.'||'STEP000';
-SET  &NOWTIME    = HHMMSS(A8);
-TYPE &NOWTIME.EVAL &STEP.EVAL
-TYPE &NOWTIME.EVAL *** Establish APP PATH and defaults
-RUN
-*-----------------------------------------------------------------------------
-INCLUDE EPP2/EPP_DEFAULTS
-*-----------------------------------------------------------------------------
-SET  &STEP    = '&FOCFEXNAME.EVAL'||'.'||'STEP000';
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL &STEP.EVAL COMPLETE RETCODE&RETCODE.EVAL
-TYPE -------------------------------------------------------------------------
-IF   &FOCERRNUM.EVAL NE 0 THEN GOTO ERRORS;
-*-----------------------------------------------------------------------------
-STEP001
-SET  &STEP    = '&FOCFEXNAME.EVAL'||'.'||'STEP001';
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL &STEP.EVAL
-TYPE &NOWTIME.EVAL *** Delete all rows from &CLP_DTM.EVAL&CLP_DTM_TBL.EVAL
-*-----------------------------------------------------------------------------
SQL
TRUNCATE TABLE &CLP_DTM.EVAL&CLP_DTM_TBL.EVAL;
COMMIT WORK;
END
-RUN
-*-----------------------------------------------------------------------------
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL &STEP.EVAL COMPLETE RETCODE&RETCODE.EVAL
-TYPE -------------------------------------------------------------------------
-IF   &RETCODE.EVAL NE 0 THEN GOTO ERRORS;
-*-----------------------------------------------------------------------------
-STEP002
-SET  &STEP    = '&FOCFEXNAME.EVAL'||'.'||'STEP002';
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL &STEP.EVAL
-TYPE &NOWTIME.EVAL *** Extract CLP_DTM records from 835 EDI file
-*-----------------------------------------------------------------------------
DEFINE FILE EDI835_BY_RT_LX
FILE_ID/I10                              = &FILE_ID.EVAL;
GS_GRP_SEQ_NO/I5  WITH GS_FUNC_ID_CD     = GS_GRP_SEQ_NO + 1;
ST_SEQ_NO/I7  WITH ST_TXN_SET_CNTL_NO    = IF GS_GRP_SEQ_NO        EQ LAST GS_GRP_SEQ_NO
                                              THEN ST_SEQ_NO + 1
                                           ELSE 1;
LX_SEQ_NO/I4      WITH LX_ASGND_NO       = IF    FILE_ID           EQ LAST FILE_ID
                                            AND GS_GRP_SEQ_NO      EQ LAST GS_GRP_SEQ_NO
                                            AND ST_SEQ_NO          EQ LAST ST_SEQ_NO
                                                THEN LX_SEQ_NO + 1
                                           ELSE 1;
CLP_CLM_SEQ_NO/I6 WITH CLP_CLM_SUBMTR_ID = IF   FILE_ID            EQ LAST FILE_ID
                                            AND GS_GRP_SEQ_NO      EQ LAST GS_GRP_SEQ_NO
                                            AND ST_SEQ_NO          EQ LAST ST_SEQ_NO
                                            AND LX_SEQ_NO          EQ LAST LX_SEQ_NO
                                                THEN CLP_CLM_SEQ_NO + 1
                                           ELSE 1;
CLP_DTM_SEQ_NO/I6 WITH CLP_DTM_DTTM_QLFR = IF   FILE_ID            EQ LAST FILE_ID
                                            AND GS_GRP_SEQ_NO      EQ LAST GS_GRP_SEQ_NO
                                            AND ST_SEQ_NO          EQ LAST ST_SEQ_NO
                                            AND LX_SEQ_NO          EQ LAST LX_SEQ_NO
                                            AND CLP_CLM_SEQ_NO     EQ LAST CLP_CLM_SEQ_NO
                                                THEN CLP_DTM_SEQ_NO + 1
                                           ELSE 1;
CLP_DTM_EPP_GEN_SEQ/I2                   = 0;
CLP_DTM_INCL_RSN_CD/A3                   = 'ORG';
CLP_DTM_EXCL_RSN_CD/A3                   = '   ';
END
-*
TABLE FILE EDI835_BY_RT_LX
PRINT CLP_DTM_DTTM_QLFR         AS CLP_DTM_DTTM_QLFR
      CLP_DTM_DATE              AS CLP_DTM_DATE
      CLP_DTM_TIME              AS CLP_DTM_TIME
      CLP_DTM_TIME_CD           AS CLP_DTM_TIME_CD
      CLP_DTM_DTTM_PD_FMT_QLFR  AS CLP_DTM_DTTM_PD_FMT_QLFR
      CLP_DTM_DTTM_PD           AS CLP_DTM_DTTM_PD
      CLP_DTM_INCL_RSN_CD       AS CLP_DTM_INCL_RSN_CD
      CLP_DTM_EXCL_RSN_CD       AS CLP_DTM_EXCL_RSN_CD
      ISA_IC_DATE               NOPRINT
BY    FILE_ID                   AS CLP_DTM_ISA_FILE_ID
BY    GS_GRP_SEQ_NO             AS CLP_DTM_GS_GRP_SEQ_NO
BY    ST_SEQ_NO                 AS CLP_DTM_ST_SEQ_NO
BY    LX_SEQ_NO                 AS CLP_DTM_LX_SEQ_NO
BY    CLP_CLM_SEQ_NO            AS CLP_DTM_CLP_CLM_SEQ_NO
BY    CLP_DTM_SEQ_NO            AS CLP_DTM_SEQ_NO
BY    CLP_DTM_EPP_GEN_SEQ       AS CLP_DTM_EPP_GEN_SEQ
WHERE CLP_DTM_DTTM_QLFR IS NOT MISSING;
ON TABLE SET ALL ON
ON TABLE SET ASNAMES ON
ON TABLE SET HOLDLIST PRINTONLY
ON TABLE HOLD AS 835RECS
END
-RUN
-*-----------------------------------------------------------------------------
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL RECORDS &RECORDS.EVAL LINES &LINES.EVAL
-TYPE &NOWTIME.EVAL &STEP.EVAL COMPLETE RETCODE&RETCODE.EVAL
-TYPE -------------------------------------------------------------------------
-IF   &RETCODE.EVAL NE 0 THEN GOTO ERRORS;
-IF   &LINES.EVAL   EQ 0 THEN GOTO THE_END;
-*-----------------------------------------------------------------------------
-STEP003
-SET  &STEP    = '&FOCFEXNAME.EVAL'||'.'||'STEP003';
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL &STEP.EVAL
-TYPE &NOWTIME.EVAL *** Insert CLP_DTM rows to &CLP_DTM.EVAL&CLP_DTM_TBL.EVAL
-*-----------------------------------------------------------------------------
MODIFY FILE &CLP_DTM_TBL.EVAL
SQL SET LOADONLY
MATCH CLP_DTM_ISA_FILE_ID CLP_DTM_GS_GRP_SEQ_NO CLP_DTM_ST_SEQ_NO CLP_DTM_LX_SEQ_NO CLP_DTM_CLP_CLM_SEQ_NO CLP_DTM_SEQ_NO CLP_DTM_EPP_GEN_SEQ
ON MATCH   REJECT
ON NOMATCH INCLUDE
FIXFORM FROM 835RECS
DATA ON 835RECS
LOG FORMAT ON LOGFMT MSG OFF
END
-RUN
-*-----------------------------------------------------------------------------
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL TRANS &TRANS.EVAL INPUT &INPUT.EVAL
-TYPE &NOWTIME.EVAL &STEP.EVAL COMPLETE RETCODE&RETCODE.EVAL
-TYPE -------------------------------------------------------------------------
-IF   &RETCODE.EVAL NE 0           THEN GOTO ERRORS;
-IF   &INPUT.EVAL   NE &TRANS.EVAL THEN GOTO ERRORS;
-*-----------------------------------------------------------------------------
-GOTO THE_END;
-TYPE -------------------------------------------------------------------------
-ERRORS
-SET &ERRFEXNAME = '&FOCFEXNAME.EVAL';
-SET &ERRSTEP    = '&STEP.EVAL';
-SET &ERRRETCODE = &RETCODE.EVAL;
-SET &ERRLINES   = &LINES.EVAL;
-SET &ERRFOCERR  = &FOCERRNUM.EVAL;
-SET &ERROR_IND  = 'ERROR';
-RUN
-******************************************************************************
-THE_END
-******************************************************************************
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL FINISH &FOCFEXNAME.EVAL
-TYPE *************************************************************************
-RUN
