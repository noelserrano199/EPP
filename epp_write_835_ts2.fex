-******************************************************************************
-* Program:
-* Title:
-* Author:       Bill Schwartz
-* Date:
-* Description:
-*
-******************************************************************************
-*
-* Tables Accessed:
-*
-* Tables Updated:
-*
-******************************************************************************
-*
-* Include/EX Files:
-*
-******************************************************************************
-*
-* Files Allocated:
-*
-******************************************************************************
-*
-* Change History:
-*
-*     Programmer             Date              Description of Change
-*     ------------------  ----------           -------------------------------
-*
-******************************************************************************
-*-SET &ECHO = ALL;
-TYPE *************************************************************************
-SET  &NOWTIME = HHMMSS(A8);
-RUN
-TYPE &NOWTIME.EVAL START &FOCFEXNAME.EVAL
-TYPE -------------------------------------------------------------------------
-IF  &DFLTCNTL.EXISTS EQ 1 THEN GOTO STEP001;
-******************************************************************************
-STEP000
-SET  &STEP       = '&FOCFEXNAME.EVAL'||'.'||'STEP000';
-SET  &NOWTIME    = HHMMSS(A8);
-TYPE &NOWTIME.EVAL &STEP.EVAL
-TYPE &NOWTIME.EVAL *** Establish APP PATH and defaults
-RUN
-*-----------------------------------------------------------------------------
-INCLUDE EPP2/EPP_DEFAULTS
-*-----------------------------------------------------------------------------
-SET  &STEP    = '&FOCFEXNAME.EVAL'||'.'||'STEP000';
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL &STEP.EVAL COMPLETE RETCODE&RETCODE.EVAL
-TYPE -------------------------------------------------------------------------
-IF   &FOCERRNUM.EVAL NE 0 THEN GOTO ERRORS;
-*-----------------------------------------------------------------------------
-STEP001
-SET  &STEP    = '&FOCFEXNAME.EVAL'||'.'||'STEP001';
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL &STEP.EVAL
-TYPE &NOWTIME.EVAL *** Write CLP 835 Transactions
-*-----------------------------------------------------------------------------
SQL
SELECT
      'CLP' "CLP"
     ,CLP_CLM_SUBMTR_ID
     ,CLP_CLM_STATUS_CD
     ,CLP_TOT_CLM_CRG_AMT
     ,CLP_CLM_PYMT_AMT
     ,CLP_PT_RESP_AMT
     ,CLP_CLM_FIL_ID_CD
     ,CLP_PYR_CLM_CNTL_NO
     ,CLP_FCLTY_CD_VALU
     ,CLP_CLM_FREQ_TYP_CD
     ,CLP_PT_STS_CD
     ,CLP_DRG_CD
     ,CLP_QTY
     ,CLP_PCT
     ,CLP_YN_COND_RESP_CD
     ,CLP_CLM_SEQ_NO
     ,CLP_ST_TXN_SET_CNTL_NO
     ,CLP_GS_GRP_SEQ_NO
     ,CLP_LX_SEQ_NO
     ,ST_SEQ_NO
FROM  &ST.EVAL&ST_TBL.EVAL
INNER JOIN
      &CLP.EVAL&CLP_TBL.EVAL
ON    CLP_GS_GRP_SEQ_NO      = ST_GS_GRP_SEQ_NO
  AND CLP_ST_TXN_SET_CNTL_NO = ST_TXN_SET_CNTL_NO
  AND CLP_EXCL_RSN_CD        IN(' ','   ')
WHERE ST_EXCL_RSN_CD         = '   '
ORDER BY
      CLP_GS_GRP_SEQ_NO
     ,ST_SEQ_NO
     ,CLP_LX_SEQ_NO
     ,CLP_CLM_SEQ_NO
;
TABLE FILE SQLOUT
PRINT
  COMPUTE C_REQ_TYPE/A4= 'CLP*';
  COMPUTE C_GS_SEQ_NO/A5 = CLP_GS_GRP_SEQ_NO;
  COMPUTE C_ST_CNTL_NO/A9 = ST_SEQ_NO;
  COMPUTE C_REC_SEQ_IND/A2 = '50';
  COMPUTE C_LX_SEQ_NO/A5 = CLP_LX_SEQ_NO;
  COMPUTE C_CLP_SEQ_NO/A5 = CLP_CLM_SEQ_NO;
  COMPUTE C_REC_SEQ_CLM/A2 = '12';
  COMPUTE C_SVC_SEQ_NO/A5 = '0';
  COMPUTE C_SERV_SEQ_IND/A2 = '00';
  COMPUTE C_SVC_CAS_SEQ_NO/A5 = '0';
  COMPUTE C_EDIVAL/A600V = C_REQ_TYPE || CLP_CLM_SUBMTR_ID || '*' ||
                          CLP_CLM_STATUS_CD || '*' || CLP_TOT_CLM_CRG_AMT || '*' ||
                          CLP_CLM_PYMT_AMT || '*' || CLP_PT_RESP_AMT || '*' ||
                          CLP_CLM_FIL_ID_CD || '*' || CLP_PYR_CLM_CNTL_NO || '*' ||
                          CLP_FCLTY_CD_VALU || '*' || CLP_CLM_FREQ_TYP_CD || '*' ||
                          CLP_PT_STS_CD || '*' ||  CLP_DRG_CD || '*' ||
                          CLP_QTY || '*' ||  CLP_PCT || '*' ||
                          CLP_YN_COND_RESP_CD; NOPRINT
  COMPUTE C_RMVSPC/A600V = TRIMV('T', C_EDIVAL, 600, ' ', 1, C_RMVSPC); NOPRINT
  COMPUTE C_EDIREC/A600V = TRIMV('T', C_RMVSPC, 600, '*', 1, C_EDIREC) | '~';
ON TABLE SET HOLDLIST PRINTONLY
ON TABLE HOLD AS EDITEMP FORMAT ALPHA
END
-RUN
-*-----------------------------------------------------------------------------
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL RECORDS &RECORDS.EVAL LINES &LINES.EVAL
-TYPE &NOWTIME.EVAL &STEP.EVAL COMPLETE RETCODE&RETCODE.EVAL
-TYPE -------------------------------------------------------------------------
-IF   &RETCODE.EVAL NE 0 THEN GOTO ERRORS;
-*-----------------------------------------------------------------------------
-GOTO THE_END;
-TYPE -------------------------------------------------------------------------
-ERRORS
-SET &ERRFEXNAME = '&FOCFEXNAME.EVAL';
-SET &ERRSTEP    = '&STEP.EVAL';
-SET &ERRRETCODE = &RETCODE.EVAL;
-SET &ERRLINES   = &LINES.EVAL;
-SET &ERRFOCERR  = &FOCERRNUM.EVAL;
-SET &ERROR_IND  = 'ERROR';
-RUN
-*-INCLUDE GBL_ERRORS
-******************************************************************************
-THE_END
-******************************************************************************
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL FINISH &FOCFEXNAME.EVAL
-TYPE *************************************************************************
-RUN
