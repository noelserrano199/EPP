-******************************************************************************-* Program:
-* Title:
-* Author:       Bill Schwartz
-* Date:
-* Description:
-******************************************************************************
-* Tables Accessed:
-* Tables Updated:
-******************************************************************************
-* Include/EX Files:
-******************************************************************************
-* Files Allocated:
-******************************************************************************
-* Change History:
-*  Programmer      Date      Description of Change
-* ------------  ----------  --------------------------------------------------
-* WLS           08/09/2013  Changed WHERE TOTAL in STEP002 to WHERE
-******************************************************************************
-*-SET &ECHO = ALL;
-TYPE *************************************************************************
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL START &FOCFEXNAME.EVAL
-TYPE -------------------------------------------------------------------------
-IF  &DFLTCNTL.EXISTS EQ 1 THEN GOTO STEP001;
-******************************************************************************
-STEP000
-SET  &STEP       = '&FOCFEXNAME.EVAL'||'.'||'STEP000';
-SET  &NOWTIME    = HHMMSS(A8);
-TYPE &NOWTIME.EVAL &STEP.EVAL
-TYPE &NOWTIME.EVAL *** Establish APP PATH and defaults
-RUN
-*-----------------------------------------------------------------------------
-INCLUDE EPP2/EPP_DEFAULTS
-*-----------------------------------------------------------------------------
-SET  &STEP    = '&FOCFEXNAME.EVAL'||'.'||'STEP000';
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL &STEP.EVAL COMPLETE RETCODE&RETCODE.EVAL
-TYPE -------------------------------------------------------------------------
-IF   &FOCERRNUM.EVAL NE 0 THEN GOTO ERRORS;
-*-----------------------------------------------------------------------------
-STEP001
-SET  &STEP    = '&FOCFEXNAME.EVAL'||'.'||'STEP001';
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL &STEP.EVAL
-TYPE &NOWTIME.EVAL *** Delete all rows from &SVC_CAS.EVAL&SVC_CAS_TBL.EVAL
-*-----------------------------------------------------------------------------
SQL
TRUNCATE TABLE &SVC_CAS.EVAL&SVC_CAS_TBL.EVAL;
COMMIT WORK;
END
-RUN
-*-----------------------------------------------------------------------------
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL &STEP.EVAL COMPLETE RETCODE&RETCODE.EVAL
-TYPE -------------------------------------------------------------------------
-IF   &RETCODE.EVAL NE 0 THEN GOTO ERRORS;
-*-----------------------------------------------------------------------------
-STEP002
-SET  &STEP    = '&FOCFEXNAME.EVAL'||'.'||'STEP002';
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL &STEP.EVAL
-TYPE &NOWTIME.EVAL *** Extract SVC_CAS records from 835 EDI file
-*-----------------------------------------------------------------------------
DEFINE FILE EDI835_BY_RT_LX
FILE_ID/I10                              = &FILE_ID.EVAL;
GS_GRP_SEQ_NO/I5  WITH GS_FUNC_ID_CD     = GS_GRP_SEQ_NO + 1;
ST_SEQ_NO/I7 WITH ST_TXN_SET_CNTL_NO     = IF GS_GRP_SEQ_NO        EQ LAST GS_GRP_SEQ_NO
                                              THEN ST_SEQ_NO + 1
                                           ELSE 1;
LX_SEQ_NO/I4      WITH LX_ASGND_NO       = IF   FILE_ID            EQ LAST FILE_ID
                                            AND GS_GRP_SEQ_NO      EQ LAST GS_GRP_SEQ_NO
                                            AND ST_TXN_SET_CNTL_NO EQ LAST ST_TXN_SET_CNTL_NO
                                                THEN LX_SEQ_NO + 1
                                           ELSE 1;
CLP_CLM_SEQ_NO/I6 WITH CLP_CLM_SUBMTR_ID = IF   FILE_ID            EQ LAST FILE_ID
                                            AND GS_GRP_SEQ_NO      EQ LAST GS_GRP_SEQ_NO
                                            AND ST_SEQ_NO          EQ LAST ST_SEQ_NO
                                            AND LX_SEQ_NO          EQ LAST LX_SEQ_NO
                                                THEN CLP_CLM_SEQ_NO + 1
                                           ELSE 1;
SVC_SEQ_NO/I5    WITH SVC_COMP_MED_PROC1 = IF    FILE_ID            EQ LAST FILE_ID
                                             AND GS_GRP_SEQ_NO      EQ LAST GS_GRP_SEQ_NO
                                             AND ST_SEQ_NO          EQ LAST ST_SEQ_NO
                                             AND LX_SEQ_NO          EQ LAST LX_SEQ_NO
                                             AND CLP_CLM_SEQ_NO     EQ LAST CLP_CLM_SEQ_NO
                                                 THEN SVC_SEQ_NO + 1
                                            ELSE 1;
SVC_CAS_SEQ_NO/I5 WITH SVC_CAS_CLM_ADJ_AMT1=  IF FILE_ID            EQ LAST FILE_ID
                                             AND GS_GRP_SEQ_NO      EQ LAST GS_GRP_SEQ_NO
                                             AND ST_SEQ_NO          EQ LAST ST_SEQ_NO
                                             AND LX_SEQ_NO          EQ LAST LX_SEQ_NO
                                             AND CLP_CLM_SEQ_NO     EQ LAST CLP_CLM_SEQ_NO
                                             AND SVC_SEQ_NO         EQ LAST SVC_SEQ_NO
                                                 THEN SVC_CAS_SEQ_NO + 1
                                            ELSE 1;
SVC_CAS_EPP_GEN_SEQ/I2                     = 0;
SVC_CAS_INCL_RSN_CD/A3                     = 'ORG';
SVC_CAS_EXCL_RSN_CD/A3                     = '   ';
SVC_CAS_PATIENT_PYMT/A18                   = '0';
END
-*
TABLE FILE EDI835_BY_RT_LX
PRINT SVC_CAS_CLM_ADJ_GRP_CD1   AS SVC_CAS_CLM_ADJ_GRP_CD1
      SVC_CAS_CLM_ADJ_RSN_CD1   AS SVC_CAS_CLM_ADJ_RSN_CD1
      SVC_CAS_CLM_ADJ_AMT1      AS SVC_CAS_CLM_ADJ_AMT1
      SVC_CAS_CLM_ADJ_QTY1      AS SVC_CAS_CLM_ADJ_QTY1
      SVC_CAS_CLM_ADJ_RSN_CD2   AS SVC_CAS_CLM_ADJ_RSN_CD2
      SVC_CAS_CLM_ADJ_AMT2      AS SVC_CAS_CLM_ADJ_AMT2
      SVC_CAS_CLM_ADJ_QTY2      AS SVC_CAS_CLM_ADJ_QTY2
      SVC_CAS_CLM_ADJ_RSN_CD3   AS SVC_CAS_CLM_ADJ_RSN_CD3
      SVC_CAS_CLM_ADJ_AMT3      AS SVC_CAS_CLM_ADJ_AMT3
      SVC_CAS_CLM_ADJ_QTY3      AS SVC_CAS_CLM_ADJ_QTY3
      SVC_CAS_CLM_ADJ_RSN_CD4   AS SVC_CAS_CLM_ADJ_RSN_CD4
      SVC_CAS_CLM_ADJ_AMT4      AS SVC_CAS_CLM_ADJ_AMT4
      SVC_CAS_CLM_ADJ_QTY4      AS SVC_CAS_CLM_ADJ_QTY4
      SVC_CAS_CLM_ADJ_RSN_CD5   AS SVC_CAS_CLM_ADJ_RSN_CD5
      SVC_CAS_CLM_ADJ_AMT5      AS SVC_CAS_CLM_ADJ_AMT5
      SVC_CAS_CLM_ADJ_QTY5      AS SVC_CAS_CLM_ADJ_QTY5
      SVC_CAS_CLM_ADJ_RSN_CD6   AS SVC_CAS_CLM_ADJ_RSN_CD6
      SVC_CAS_CLM_ADJ_AMT6      AS SVC_CAS_CLM_ADJ_AMT6
      SVC_CAS_CLM_ADJ_QTY6      AS SVC_CAS_CLM_ADJ_QTY6
	  SVC_CAS_INCL_RSN_CD       AS SVC_CAS_INCL_RSN_CD
      SVC_CAS_EXCL_RSN_CD       AS SVC_CAS_EXCL_RSN_CD
	  SVC_CAS_PATIENT_PYMT      AS SVC_CAS_PATIENT_PYMT
      ISA_IC_DATE               NOPRINT
BY    FILE_ID                   AS SVC_CAS_ISA_FILE_ID
BY    GS_GRP_SEQ_NO             AS SVC_CAS_GS_GRP_SEQ_NO
BY    ST_SEQ_NO                 AS SVC_CAS_ST_SEQ_NO
BY    LX_SEQ_NO                 AS SVC_CAS_LX_SEQ_NO
BY    CLP_CLM_SEQ_NO            AS SVC_CAS_CLP_CLM_SEQ_NO
BY    SVC_SEQ_NO                AS SVC_CAS_SVC_SEQ_NO
BY    SVC_CAS_SEQ_NO            AS SVC_CAS_SEQ_NO
BY    SVC_CAS_EPP_GEN_SEQ       AS SVC_CAS_EPP_GEN_SEQ
WHERE SVC_CAS_CLM_ADJ_AMT1 IS NOT MISSING;
WHERE SVC_SEQ_NO           IS NOT MISSING;
ON TABLE SET ALL ON
ON TABLE SET ASNAMES ON
ON TABLE SET HOLDLIST PRINTONLY
ON TABLE HOLD AS 835RECS
END
-RUN
-*-----------------------------------------------------------------------------
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL RECORDS &RECORDS.EVAL LINES &LINES.EVAL
-TYPE &NOWTIME.EVAL &STEP.EVAL COMPLETE RETCODE&RETCODE.EVAL
-TYPE -------------------------------------------------------------------------
-IF   &RETCODE.EVAL NE 0 THEN GOTO ERRORS;
-TYPE If LINES equal 0 the goto THE_END
-IF   &LINES.EVAL   EQ 0 THEN GOTO THE_END;
-*-----------------------------------------------------------------------------
-STEP003
-SET  &STEP    = '&FOCFEXNAME.EVAL'||'.'||'STEP003';
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL &STEP.EVAL
-TYPE &NOWTIME.EVAL *** Insert SVC_CAS rows to &SVC_CAS.EVAL&SVC_CAS_TBL.EVAL
-*-----------------------------------------------------------------------------
MODIFY FILE &SVC_CAS_TBL.EVAL
SQL SET LOADONLY
MATCH SVC_CAS_ISA_FILE_ID SVC_CAS_GS_GRP_SEQ_NO SVC_CAS_ST_SEQ_NO SVC_CAS_LX_SEQ_NO SVC_CAS_CLP_CLM_SEQ_NO SVC_CAS_SVC_SEQ_NO SVC_CAS_SEQ_NO SVC_CAS_EPP_GEN_SEQ
ON MATCH   REJECT
ON NOMATCH INCLUDE
FIXFORM FROM 835RECS
DATA ON 835RECS
LOG FORMAT ON LOGFMT MSG OFF
END
-RUN
-*-----------------------------------------------------------------------------
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL TRANS &TRANS.EVAL INPUT &INPUT.EVAL
-TYPE &NOWTIME.EVAL &STEP.EVAL COMPLETE RETCODE&RETCODE.EVAL
-TYPE -------------------------------------------------------------------------
-IF   &RETCODE.EVAL NE 0           THEN GOTO ERRORS;
-IF   &INPUT.EVAL   NE &TRANS.EVAL THEN GOTO ERRORS;
-*-----------------------------------------------------------------------------
-GOTO THE_END;
-TYPE -------------------------------------------------------------------------
-ERRORS
-SET &ERRFEXNAME = '&FOCFEXNAME.EVAL';
-SET &ERRSTEP    = '&STEP.EVAL';
-SET &ERRRETCODE = &RETCODE.EVAL;
-SET &ERRLINES   = &LINES.EVAL;
-SET &ERRFOCERR  = &FOCERRNUM.EVAL;
-SET &ERROR_IND  = 'ERROR';
-RUN
-******************************************************************************
-THE_END
-******************************************************************************
-SET  &NOWTIME = HHMMSS(A8);
-TYPE &NOWTIME.EVAL FINISH &FOCFEXNAME.EVAL
-TYPE *************************************************************************
-RUN
